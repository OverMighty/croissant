plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'com.github.overmighty'
version '1.0.2'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
}

dependencies {
    implementation 'com.destroystokyo.paper:paper-api:1.15.1-R0.1-SNAPSHOT'

    testImplementation 'org.mockito:mockito-core:3.2.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

test {
    useJUnitPlatform()

    testLogging {
        events 'skipped', 'failed'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.enabled = true
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']

    pkg {
        repo = 'maven'
        name = project.name
        desc = 'Lightweight yet useful library for Bukkit/Spigot/Paper plugins.'
        websiteUrl = 'https://github.com/OverMighty/croissant'
        issueTrackerUrl = 'https://github.com/OverMighty/croissant/issues'
        vcsUrl = 'https://github.com/OverMighty/croissant.git'
        githubRepo = 'OverMighty/croissant'
        licenses = ['MIT']

        version {
            name = project.version

            gpg {
                sign = true
            }

            mavenCentralSync {
                user = System.getenv('OSSRH_USER')
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Croissant'
                description = bintray.pkg.desc
                url = bintray.pkg.websiteUrl

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/OverMighty/croissant/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'OverMighty'
                        email = 'its.overmighty@gmail.com'
                        url = 'https://github.com/OverMighty'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/OverMighty/croissant.git'
                    developerConnection = 'scm:git:ssh://github.com:OverMighty/croissant.git'
                    url = 'http://github.com/OverMighty/croissant/tree/master'
                }
            }
        }
    }
}
